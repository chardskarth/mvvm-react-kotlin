buildscript {
	apply from: "versions.gradle"

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}")
	}
}
repositories {
	mavenCentral()
}

subprojects {
	repositories {
		maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
		maven { url "https://kotlin.bintray.com/kotlinx" }
		maven { url "http://dl.bintray.com/kotlin/kotlin-js-wrappers" }
		jcenter()
	}

	def projectName = name

	ext.applyKotlinJS = {
		apply plugin: 'kotlin2js'

		sourceSets {
			main {
				kotlin {
					srcDirs = ["$projectDir/src/main/kotlin"]
				}
			}
		}

		dependencies {
			compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
			compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
			compile 'org.jetbrains:kotlin-react:16.6.0-pre.68-kotlin-1.3.20'
			compile 'org.jetbrains:kotlin-react-dom:16.6.0-pre.68-kotlin-1.3.20'
			compile 'org.jetbrains:kotlin-react-router-dom:4.3.1-pre.68-kotlin-1.3.20'
			compile 'org.jetbrains:kotlin-extensions:1.0.1-pre.68-kotlin-1.3.20'
			testCompile "org.jetbrains.kotlin:kotlin-test-js"
		}

		compileKotlin2Js {
			kotlinOptions {
				outputFile = "$projectDir/build/classes/main/${projectName}.js"
				moduleKind = "commonjs"
				sourceMap = true
				sourceMapEmbedSources = "always"
			}
		}
	}
}

project(':server-app') {
	apply plugin: 'war'
	apply plugin: 'kotlin'
	apply plugin: 'kotlin-spring'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
}
